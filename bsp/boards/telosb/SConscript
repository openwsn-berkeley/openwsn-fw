import os

Import('env')

localEnv = env.Clone()

# scons doesn't let us look to parent directories for source, so the
# bsp/chips/cc2420/radio.c is off limits from this file. To keep things
# simple, each SConscript file in bsp/chips/* will return a list of objects
# which can be appended to the source list. Don't forget to specify a variant_dir,
# or else the build will occur directly in the chips directory.

cc2420 = localEnv.SConscript(
    os.path.join('#','bsp','chips','cc2420','SConscript'),
    variant_dir = 'cc2420',
    exports     = {'env': env},
)

sht11 = localEnv.SConscript(
    os.path.join('#','bsp','chips','sht11','SConscript'),
    variant_dir = 'sht11',
    exports     = {'env': env},
)

source = [
    'board.c',
    'sctimer.c',
    'debugpins.c',
    'eui64.c',
    'leds.c',
    'spi.c',
    'uart.c',
    'adc_sensor.c',
    'sensors.c'
]

localEnv.Append(
        CPPPATH =  [
            os.path.join('#','bsp','chips','cc2420'),
            os.path.join('#','bsp','chips','sht11'),            
        ],
)

board  = localEnv.Object(source=source) + cc2420 + sht11

Return('board')
