import os

Import('env')

localEnv = env.Clone()

# scons doesn't let us look to parent directories for source, so the
# bsp/chips/at86rf231/radio.c is off limits from this file. To keep things
# simple, each SConscript file in bsp/chips/* will return a list of objects
# which can be appended to the source list. Don't forget to specify a variant_dir,
# or else the build will occur directly in the chips directory.

adxl346 = localEnv.SConscript(
    os.path.join('#','bsp','chips','adxl346','SConscript'),
    variant_dir = 'adxl346',
    exports     = {'env': env},
)

max44009 = localEnv.SConscript(
    os.path.join('#','bsp','chips','max44009','SConscript'),
    variant_dir = 'max44009',
    exports     = {'env': env},
)

sht21 = localEnv.SConscript(
    os.path.join('#','bsp','chips','sht21','SConscript'),
    variant_dir = 'sht21',
    exports     = {'env': env},
)

source   = \
    [file for file in Glob('*.c') if file.name.find('iar')==-1] + \
    Glob('source/*.c')

localEnv.Append(
    CPPPATH =  [
        os.path.join('#','bsp','boards','OpenMote-CC2538','headers'),
        os.path.join('#','bsp','chips','adxl346'),
        os.path.join('#','bsp','chips','max44009'),
        os.path.join('#','bsp','chips','sht21'),
    ],
)

board  = localEnv.Object(source=source) + adxl346 + max44009 + sht21

Return('board')
