import os

Import('env')

localEnv = env.Clone()

# scons doesn't let us look to parent directories for source, so the
# bsp/chips/cc1101/radio.c is off limits from this file. To keep things
# simple, each SConscript file in bsp/chips/* will return a list of objects
# which can be appended to the source list. Don't forget to specify a
# variant_dir, or else the build will occur directly in the chips directory.

cc1101 = localEnv.SConscript(
    os.path.join('#','bsp','chips','cc1101','SConscript'),
    variant_dir    = 'cc1101',
    exports        = {'env': env},
)

source = [
    'board.c',
    'sctimer.c',
    'debugpins.c',
    'eui64.c',
    'leds.c',
    'spi.c',
    'uart.c',
    'cryptoengine.c',
]

board  = localEnv.Object(source=source) + cc1101

Return('board')
