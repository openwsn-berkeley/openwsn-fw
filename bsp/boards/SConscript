import os
import distutils.sysconfig

Import('env')

localEnv = env.Clone()

sources_h = [
    'board.h',
    'cryptoengine.h',
    'debugpins.h',
    'eui64.h',
    'leds.h',
    'radio.h',
    'uart.h',
    'sctimer.h',
    os.path.join('common', 'openccms.h'),
    os.path.join('common', 'openaes.h'),
]

sources_c = [
    os.path.join('common', 'openccms.c'),
    os.path.join('common', 'openaes.c'),
]

target = 'libbsp'

#============================ SCons targets ===================================

if localEnv['board']=='python':
    
    actions = []
    
    for s in sources_c+sources_h:
        action = localEnv.Objectify(
            target = localEnv.ObjectifiedFilename(s),
            source = s,
        )
        actions += [action]
        
    Alias('objectifybspheaders', actions)
    
    common = [localEnv.ObjectifiedFilename(s) for s in sources_c]
    
else:
    common = sources_c
    
common = localEnv.Object(source=common)
        
if env['toolchain']!='iar-proj':
        
    board = localEnv.SConscript(
        os.path.join('#','bsp','boards',localEnv['board'],'SConscript'),
        variant_dir = localEnv['board'],
        exports     = {'env': env},
    )

    libbsp = localEnv.Library(
        target = target,
        source = [board,common],
    )
    Alias(target, libbsp)
