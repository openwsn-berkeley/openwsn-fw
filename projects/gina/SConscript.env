import os

Import('env')

# create build environment
buildEnv = env.Clone()

# inherit environment from user (PATH, etc)
buildEnv['ENV'] = os.environ

# choose bsp. Normally this would be the same as the board name,
# however, there are cases where one might want to make separate build
# configuration for the same board.
buildEnv['BSP'] = buildEnv['board']

# include board/bsp-specific directories
buildEnv.Append(
   CPPPATH = [
      os.path.join('#','bsp','boards',buildEnv['board']),
   ]
)

MSPVERSION = "2618"
if   buildEnv['toolchain']=='mspgcc':
   flags = []
   flags += ['-mmcu=msp430f{0}'.format(MSPVERSION)]
   if buildEnv['debug']==0:
      flags += ['-Os']
   else:
      flags += ['-O0']
      flags += ['-g']
   buildEnv.Append(CCFLAGS   = ' '.join(flags))
   buildEnv.Append(LINKFLAGS = ' '.join(flags))
elif buildEnv['toolchain']=='iar':
   buildEnv.Append(CCFLAGS        = '-D__MSP430F{0}__'.format(MSPVERSION))
   buildEnv.Append(LINKFLAGS      = '-f "'+env['IAR_EW430_INSTALLDIR']+'\\430\\config\\linker\\lnk430F{0}.xcl"'.format(MSPVERSION))
   
Return('buildEnv')
