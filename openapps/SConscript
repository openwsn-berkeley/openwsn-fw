import os

Import('env')

localEnv = env.Clone()

#===== retrieve the list of apps to build

apps = [
    'cjoin',
    'c6t',
    'cinfo',
    'cled',
    'cwellknown',
    'uecho',
    'uinject',
    'userialbridge',
    'uexpiration',
    'uexpiration_monitor',
    'rrt',
    'cexample',
    'cstorm'
]

#===== pick the correct source files

target = 'libopenapps'

sources_c = ['openapps.c']
sources_h = ['openapps.h']

for a in apps:
    sources_c     += [os.path.join(a, a + '.c')]
    sources_h     += [os.path.join(a, a + '.h')]

sources_c += [os.path.join("cjoin", "cojp_cbor.c")]
sources_h += [os.path.join("cjoin", "cojp_cbor.h")]

#===== build the openapps library

if localEnv['board']=='python':
    
    for s in sources_c+sources_h:
        temp = localEnv.Objectify(
            target = localEnv.ObjectifiedFilename(s),
            source = s,
        )

    libopenapps = localEnv.Library(
        target = target,
        source = [localEnv.ObjectifiedFilename(s) for s in sources_c],
    )
    localEnv.Depends(libopenapps, [localEnv.ObjectifiedFilename(s) for s in sources_h])
    
else:
    
    localEnv.Append(
        CPPPATH = [
            # inc
            os.path.join('#','inc'),
            # kernel
            os.path.join('#','kernel'),
            # openstack
            os.path.join('#', 'openstack', '02a-MAClow'),
            os.path.join('#', 'openstack', '02b-MAChigh'),
            os.path.join('#', 'openstack', '03a-IPHC'),
            os.path.join('#', 'openstack', '03b-IPv6'),
            os.path.join('#', 'openstack', '04-TRAN'),
            os.path.join('#', 'openstack', 'cross-layers'),
            # openapps
            os.path.join('#', 'openweb', 'opencoap'),
            # openapps
            os.path.join('#', 'openapps'),
        ] + [os.path.join('#', 'openapps', a) for a in apps]
    )

    libopenapps = localEnv.Library(
        target = target,
        source = sources_c,
    )


Alias('libopenapps', libopenapps)
